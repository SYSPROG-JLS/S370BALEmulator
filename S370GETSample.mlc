         BALR  R12,0 
         USING *,R12
*
*        OPEN FILE
* register 0 points to file name to open
* register 1 byte 0 = file handle number; byte 1 = r/w indicator; bytes 2-3 = file name length
         LA    R0,FNAME
         L     R1,FSPEC
         SVC   249
*
LOOP     EQU   *
*
*        GET RECORD
* register 0 points to data area
* register 1 byte 0 = file handle number
* on return from the SVC, R15 either contains
* the length of the record read or a 0 indicating EOF
         LA    R0,DATAF
         L     R1,FSPEC
         SVC   247
*
         C     R15,=F'0'
         BE    ATEOF
*
*        OUTPUT REC READ TO SCREEN
         LA    R0,DATAF
         LR    R1,R15
         SVC   255
*
         B     LOOP
*
ATEOF    EQU   *
*
*        CLOSE FILE
* register 1 byte 0 = file handle number
         L     R1,FSPEC
         SVC   248
*
         LA    R15,0
         BR    R14
         DS    0D
*
* FSPEC format:
*              byte 0 = file handle number; 00 - 99 file handle (decimal only)
*              byte 1 = r/w indicator; 00 = open for read; 01 = open for write
*              bytes 2-3 = file name length
*              byte:0 1 2 3
FSPEC    DC    XL4'0100000C'
FNAME    DC    CL12'S370TEST.TXT'
*
DATAF    DS    CL80
*
         DC    XL1'00'
*        
         LTORG
R0       EQU   0
R1       EQU   1 
R2       EQU   2 
R3       EQU   3
R4       EQU   4
R5       EQU   5 
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14 
R15      EQU   15
         END